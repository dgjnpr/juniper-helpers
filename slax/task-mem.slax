/*
*    NAME: task-mem.slax
*    VERSION: 1.0
*    PURPOSE: task-mem.slax - Write the value of current task memory utilization (kB)
*          to snmp every 5 minutes
*    AUTHOR: Francis Luong (Franco) -- @francisluong - http://about.me/francisluong
*
* To use, copy the file to /var/db/scripts/event on each routing-engine
* and then add the following configuration:
*
event-options {
    event-script {
        file task-mem.slax;
    }
}
*
*
* The following OID in the Utility MIB is used by this script:
*  jnxUtilIntegerValue.116.97.115.107.109.101.109
*  jnxUtilIntegerValue.t.a.s.k.m.e.m
* Any manipulation of the above value through restart snmp or request snmp utility-mib
* set/clear will interfere with the script.
*/

version 1.0;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns ext = "http://xmlsoft.org/XSLT/namespace";


/* Embedded event policy: set the policy to refresh every five minutes */
var $event-definition = {
    <event-options> {
        <generate-event> {
            <name> "task-mem-refresh";
            <time-interval> "300";
        }           
 
        <policy> {
            <name> "task-mem";
            <events> "task-mem-refresh";
            <then>{
                <event-script>{
                    <name> "task-mem.slax";
                }
            }
        }
    }
}

match / {
    <event-script-results> {
        var $connection = jcs:open();
        var $rpc = <get-task-memory-information>;
        var $out = jcs:execute($connection, $rpc);
        var $value = $out/task-memory-in-use-size;
    
        var $mibrpc = <request-snmp-utility-mib-set> {
            <object-type> "integer";
            <instance> "taskmem";
            <object-value> $value;  
        }
        var $res = jcs:invoke($mibrpc);
        expr jcs:close( $connection );
    }
}
